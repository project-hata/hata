
-- {-# OPTIONS --overlapping-instances #-}

module Verification.Core.Algebra.Ring.Localization.Instance.Monoid where

open import Verification.Conventions

open import Verification.Core.Data.Prop.Everything
open import Verification.Core.Setoid.Definition
open import Verification.Core.Algebra.Monoid.Definition
open import Verification.Core.Algebra.Group.Definition
-- open import Verification.Core.Algebra.Group.Quotient
open import Verification.Core.Algebra.Abelian.Definition
open import Verification.Core.Algebra.Ring.Definition
open import Verification.Core.Algebra.Ring.Localization.Definition
open import Verification.Core.Algebra.Ring.Localization.Instance.Setoid

─ = refl

  -- mytest2 : isRing ′ ⟨ R ⟩ ′
  -- mytest2 = it

  -- mytest3 : isSetoid _ (Localize R M)
  -- mytest3 = it

  -- myrefltest : ∀{a : Localize R M} -> a ∼ a
  -- myrefltest = refl {{isSetoid:Localize}}

module _ {𝑖 : 𝔏 ^ 2} {R : CRing 𝑖} {M : MCS R} where
  _⋆-Loc_ : Localize R M -> Localize R M -> Localize R M
  _⋆-Loc_ (a / da) (b / db) = (a ⋅ ⟨ db ⟩ ⋆ b ⋅ ⟨ da ⟩) / (da ⋅-MCS db)
  infixl 50 _⋆-Loc_

  ◌-Loc : Localize R M
  ◌-Loc = ◌ / ⨡-MCS

  private
    -- | ⋆ on Localize is commutative:
    lem-10 : ∀{a b : Localize R M} -> a ⋆-Loc b ∼ b ⋆-Loc a
    lem-10 {a / (da ∢ _)} {b / (db ∢ _)} = incl (⨡-MCS , P)
      where
          P : (a ⋅ db ⋆ b ⋅ da) ⋅ (db ⋅ da) ⋅ ⨡  ∼  (b ⋅ da ⋆ a ⋅ db) ⋅ (da ⋅ db) ⋅ ⨡
          P = (a ⋅ db ⋆ b ⋅ da) ⋅ (db ⋅ da) ⋅ ⨡  ≣⟨ comm-⋆ ≀⋅≀ comm-⋅ ≀⋅≀ ─ ⟩
              (b ⋅ da ⋆ a ⋅ db) ⋅ (da ⋅ db) ⋅ ⨡  ∎

    -- | ◌ is left unit
    lem-20 : ∀{a : Localize R M} -> ◌-Loc ⋆-Loc a ∼ a
    lem-20 {a / (da ∢ _)} = incl (⨡-MCS , P₅)
      where
          P₅ : ((◌ ⋅ da) ⋆ (a ⋅ ⨡)) ⋅ da ⋅ ⨡  ∼  a ⋅ (⨡ ⋅ da) ⋅ ⨡
          P₅ = ((◌ ⋅ da) ⋆ (a ⋅ ⨡)) ⋅ da ⋅ ⨡   ≣⟨ (reduce-⋅◌-l ≀⋆≀ ─) ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              (◌ ⋆ (a ⋅ ⨡)) ⋅ da ⋅ ⨡          ≣⟨ unit-l-⋆ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              (a ⋅ ⨡) ⋅ da ⋅ ⨡                ≣⟨ assoc-l-⋅ ≀⋅≀ ─ ⟩
              a ⋅ (⨡ ⋅ da) ⋅ ⨡                ∎

    -- | ◌ is right unit
    lem-30 : ∀{a : Localize R M} -> a ⋆-Loc ◌-Loc ∼ a
    lem-30 {a} = a ⋆-Loc ◌-Loc    ≣⟨ lem-10 ⟩
                 ◌-Loc ⋆-Loc a    ≣⟨ lem-20 ⟩
                 a                ∎

    -- | ⋆ is associative
    lem-40 : ∀{a b c : Localize R M} -> (a ⋆-Loc b) ⋆-Loc c ∼ a ⋆-Loc (b ⋆-Loc c)
    lem-40 {a / (da ∢ _)} {b / (db ∢ _)} {c / (dc ∢ _)} = incl (⨡-MCS , P₁)
      where
          P₀ : ((a ⋅ db ⋆ b ⋅ da) ⋅ dc ⋆ c ⋅ (da ⋅ db))  ∼  (a ⋅ (db ⋅ dc) ⋆ (b ⋅ dc ⋆ c ⋅ db) ⋅ da)
          P₀ = (a ⋅ db ⋆ b ⋅ da) ⋅ dc ⋆ c ⋅ (da ⋅ db)            ≣⟨ distr-r-⋅ ≀⋆≀ ─ ⟩
               a ⋅ db ⋅ dc ⋆ b ⋅ da ⋅ dc ⋆ c ⋅ (da ⋅ db)         ≣⟨ assoc-l-⋅ ≀⋆≀ (assoc-l-⋅ ∙ (─ ≀⋅≀ comm-⋅) ∙ assoc-r-⋅) ≀⋆≀ ((─ ≀⋅≀ comm-⋅) ∙ assoc-r-⋅) ⟩
               a ⋅ (db ⋅ dc) ⋆ (b ⋅ dc) ⋅ da ⋆ (c ⋅ db) ⋅ da     ≣⟨ assoc-l-⋆ ⟩
               a ⋅ (db ⋅ dc) ⋆ ((b ⋅ dc) ⋅ da ⋆ (c ⋅ db) ⋅ da)   ≣⟨ ─ ≀⋆≀ distr-r-⋅ ⁻¹ ⟩
               (a ⋅ (db ⋅ dc) ⋆ (b ⋅ dc ⋆ c ⋅ db) ⋅ da)          ∎

          P₁ : ((a ⋅ db ⋆ b ⋅ da) ⋅ dc ⋆ c ⋅ (da ⋅ db)) ⋅ (da ⋅ (db ⋅ dc)) ⋅ ⨡  ∼  (a ⋅ (db ⋅ dc) ⋆ (b ⋅ dc ⋆ c ⋅ db) ⋅ da) ⋅ (da ⋅ db ⋅ dc) ⋅ ⨡
          P₁ = P₀ ≀⋅≀ assoc-r-⋅ ≀⋅≀ ─

    -- | ∼ is congruence over ⋆
    lem-50 : ∀{a₀ a₁ b₀ b₁ : Localize R M} -> a₀ ∼ a₁ -> b₀ ∼ b₁ -> a₀ ⋆-Loc b₀ ∼ a₁ ⋆-Loc b₁
    lem-50 {a₀ / (da₀ ∢ Da₀)} {a₁ / (da₁ ∢ Da₁)} {b₀ / (db₀ ∢ Db₀)} {b₁ / (db₁ ∢ Db₁)} (incl (t , p)) (incl (s , q)) = incl (t ⋅-MCS s , P₃)
      where
          dt = ⟨ t ⟩
          ds = ⟨ s ⟩

          P₀ : ∀{a₀ a₁ da₀ da₁ db₀ db₁ dt ds  : ⟨ R ⟩}
              -> (p : a₀ ⋅ da₁ ⋅ dt ∼ a₁ ⋅ da₀ ⋅ dt)
              -> (a₀ ⋅ db₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))  ∼  (a₁ ⋅ db₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))
          P₀ {a₀} {a₁} {da₀} {da₁} {db₀} {db₁} {dt} {ds} p =
              {- (a₀ ⋅ db₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))  -} _  ≣⟨ ─ ≀⋅≀ assoc-r-⋅ ⟩
              {- (a₀ ⋅ db₀) ⋅ ((da₁ ⋅ db₁) ⋅ dt ⋅ ds)    -} _  ≣⟨ ─ ≀⋅≀ (assoc-l-⋅ ≀⋅≀ ─) ⟩
              {- (a₀ ⋅ db₀) ⋅ (da₁ ⋅ (db₁ ⋅ dt) ⋅ ds)    -} _  ≣⟨ ─ ≀⋅≀ (─ ≀⋅≀ comm-⋅ ≀⋅≀ ─) ⟩
              {- (a₀ ⋅ db₀) ⋅ (da₁ ⋅ (dt ⋅ db₁) ⋅ ds)    -} _  ≣⟨ ─ ≀⋅≀ (assoc-r-⋅ ≀⋅≀ ─) ⟩
              {- (a₀ ⋅ db₀) ⋅ ((da₁ ⋅ dt) ⋅ db₁ ⋅ ds)    -} _  ≣⟨ assoc-l-⋅ ⟩
              {- a₀ ⋅ (db₀ ⋅ ((da₁ ⋅ dt) ⋅ db₁ ⋅ ds))    -} _  ≣⟨ ─ ≀⋅≀ assoc-r-⋅ ⟩
              {- a₀ ⋅ (db₀ ⋅ ((da₁ ⋅ dt) ⋅ db₁) ⋅ ds)    -} _  ≣⟨ ─ ≀⋅≀ (comm-⋅ ≀⋅≀ ─) ⟩
              {- a₀ ⋅ (((da₁ ⋅ dt) ⋅ db₁) ⋅ db₀ ⋅ ds)    -} _  ≣⟨ assoc-r-⋅ ⟩
              {- (a₀ ⋅ (((da₁ ⋅ dt) ⋅ db₁) ⋅ db₀)) ⋅ ds  -} _  ≣⟨ assoc-r-⋅ ≀⋅≀ ─ ⟩
              {- (a₀ ⋅ ((da₁ ⋅ dt) ⋅ db₁) ⋅ db₀) ⋅ ds    -} _  ≣⟨ assoc-r-⋅ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₀ ⋅ (da₁ ⋅ dt) ⋅ db₁ ⋅ db₀) ⋅ ds      -} _  ≣⟨ assoc-r-⋅ ≀⋅≀ ─ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₀ ⋅ da₁ ⋅ dt ⋅ db₁ ⋅ db₀) ⋅ ds        -} _  ≣⟨ p ≀⋅≀ ─ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ da₀ ⋅ dt ⋅ db₁ ⋅ db₀) ⋅ ds        -} _  ≣⟨ assoc-l-⋅ ≀⋅≀ ─ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ (da₀ ⋅ dt) ⋅ db₁ ⋅ db₀) ⋅ ds      -} _  ≣⟨ assoc-l-⋅ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ ((da₀ ⋅ dt) ⋅ db₁) ⋅ db₀) ⋅ ds    -} _  ≣⟨ ─ ≀⋅≀ comm-⋅ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ (db₁ ⋅ (da₀ ⋅ dt)) ⋅ db₀) ⋅ ds    -} _  ≣⟨ assoc-r-⋅ ≀⋅≀ ─ ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ db₁) ⋅ (da₀ ⋅ dt) ⋅ db₀ ⋅ ds      -} _  ≣⟨ assoc-l-⋅ ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ db₁) ⋅ ((da₀ ⋅ dt) ⋅ db₀) ⋅ ds    -} _  ≣⟨ ─ ≀⋅≀ (assoc-l-⋅ ∙ (─ ≀⋅≀ comm-⋅) ∙ assoc-r-⋅) ≀⋅≀ ─ ⟩
              {- (a₁ ⋅ db₁) ⋅ ((da₀ ⋅ db₀) ⋅ dt) ⋅ ds    -} _  ≣⟨ assoc-l-⋅ ⟩
              {- (a₁ ⋅ db₁) ⋅ (((da₀ ⋅ db₀) ⋅ dt) ⋅ ds)  -} _  ≣⟨ ─ ≀⋅≀ assoc-l-⋅ ⟩
              {- (a₁ ⋅ db₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))  -} _  ∎

          -- | We switch a₀, a₁ and their ds using p
          P₁ : (a₀ ⋅ db₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))  ∼  (a₁ ⋅ db₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))
          P₁ = P₀ p

          -- | We switch b₀, b₁ and their ds using q
          P₂ : (b₀ ⋅ da₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))  ∼  (b₁ ⋅ da₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))
          P₂ = (b₀ ⋅ da₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))    ≣⟨ ─ ≀⋅≀ (comm-⋅ ≀⋅≀ comm-⋅) ⟩
              (b₀ ⋅ da₀) ⋅ ((db₁ ⋅ da₁) ⋅ (ds ⋅ dt))    ≣⟨ P₀ q ⟩
              (b₁ ⋅ da₁) ⋅ ((db₀ ⋅ da₀) ⋅ (ds ⋅ dt))    ≣⟨ ─ ≀⋅≀ (comm-⋅ ≀⋅≀ comm-⋅) ⟩
              (b₁ ⋅ da₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))    ∎

          -- | We put both proofs together
          P₃ : (a₀ ⋅ db₀ ⋆ b₀ ⋅ da₀) ⋅ (da₁ ⋅ db₁) ⋅ (dt ⋅ ds)  ∼  (a₁ ⋅ db₁ ⋆ b₁ ⋅ da₁) ⋅ (da₀ ⋅ db₀) ⋅ (dt ⋅ ds)
          P₃ = (a₀ ⋅ db₀ ⋆ b₀ ⋅ da₀) ⋅ (da₁ ⋅ db₁) ⋅ (dt ⋅ ds)     ≣⟨ assoc-l-⋅ ⟩
              (a₀ ⋅ db₀ ⋆ b₀ ⋅ da₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))   ≣⟨ distr-r-⋅ ⟩
              (a₀ ⋅ db₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))   ⋆   (b₀ ⋅ da₀) ⋅ ((da₁ ⋅ db₁) ⋅ (dt ⋅ ds))   ≣⟨ P₁ ≀⋆≀ P₂ ⟩

              (a₁ ⋅ db₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))   ⋆   (b₁ ⋅ da₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))    ≣⟨ distr-r-⋅ ⁻¹ ⟩
              (a₁ ⋅ db₁ ⋆ b₁ ⋅ da₁) ⋅ ((da₀ ⋅ db₀) ⋅ (dt ⋅ ds))   ≣⟨ assoc-r-⋅ ⟩
              (a₁ ⋅ db₁ ⋆ b₁ ⋅ da₁) ⋅ (da₀ ⋅ db₀) ⋅ (dt ⋅ ds) ∎


-- module _ {𝑖 : 𝔏 ^ 2} {R : CRing 𝑖} {M : MCS R} where
  -- abstract
  instance
    isMonoid:Localize : isMonoid ′ Localize R M ′
    isMonoid._⋆_ isMonoid:Localize = _⋆-Loc_
    isMonoid.◌ isMonoid:Localize = ◌-Loc
    isMonoid.unit-l-⋆ isMonoid:Localize = lem-20
    isMonoid.unit-r-⋆ isMonoid:Localize = lem-30
    isMonoid.assoc-l-⋆ isMonoid:Localize = lem-40
    -- isMonoid.assoc-r-⋆ isMonoid:Localize = lem-40 ⁻¹
    isMonoid._≀⋆≀_ isMonoid:Localize = lem-50


  instance
    isCommutative:Localize : isCommutative ′ Localize R M ′
    isCommutative.comm-⋆ isCommutative:Localize = lem-10


-- module _ {𝑖 : 𝔏 ^ 2} {R : CRing 𝑖} {M : MCS R} where
--   abstract
--     private
--       -- ◡-Loc : Localize R M -> Localize R M
--       -- ◡-Loc (a / da) = ◡ a / da

--       aaa : (a b : Localize R M) -> ℕ
--       aaa a b =
--         let x = (a ⋆ b)
--         in {!!}





